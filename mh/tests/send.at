# This file is part of GNU Mailutils. -*- Autotest -*-
# Copyright (C) 2010-2020 Free Software Foundation, Inc.
#
# GNU Mailutils is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License as
# published by the Free Software Foundation; either version 3, or (at
# your option) any later version.
#
# GNU Mailutils is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with GNU Mailutils.  If not, see <http://www.gnu.org/licenses/>.

AT_BANNER(send)

m4_pushdef([MH_KEYWORDS],[send])

MH_CHECK([send file],[send00 send-file],[
MH_MTSTAILOR()
AT_DATA([input],
[From: mhtester@example.net
To: gray@example.net
Subject: Send file test
------
Message body
])
send ./input || exit $?
sed -e '/^Date:/s/:.*$/: now/' -e '/^LENGTH: /s/: [[0-9]][[0-9]]*/: X/' $MAIL_DUMP
find . -name ,input
],
[0],
[MSGID: 0001
SENDER: mhtester@example.net
NRCPT: 1
RCPT[[0]]: <gray@example.net>
LENGTH: X
From: mhtester@example.net
To: <gray@example.net>
Subject: Send file test
Date: now
X-Mailer: MH (AT_PACKAGE_STRING)

Message body

./,input
])

MH_CHECK([send files],[send01 send-files],[
MH_MTSTAILOR()
AT_DATA([input.1],
[From: mhtester@example.net
To: gray@example.net
Subject: Send file test 1
------
Message body 1
])

AT_DATA([input.2],
[From: mhtester@example.net
To: gray@example.org
Subject: Send file test 2
------
Message body 2
])

MAIL_DUMP_APPEND=1
export MAIL_DUMP_APPEND

send ./input.1 ./input.2 || exit $?
sed -e '/^Date:/s/:.*$/: now/' -e '/^LENGTH: /s/: [[0-9]][[0-9]]*/: X/' $MAIL_DUMP
find . -name ',input.[[12]]' | sort
],
[0],
[MSGID: 0001
SENDER: mhtester@example.net
NRCPT: 1
RCPT[[0]]: <gray@example.net>
LENGTH: X
From: mhtester@example.net
To: <gray@example.net>
Subject: Send file test 1
Date: now
X-Mailer: MH (AT_PACKAGE_STRING)

Message body 1

MSGID: 0001
SENDER: mhtester@example.net
NRCPT: 1
RCPT[[0]]: <gray@example.org>
LENGTH: X
From: mhtester@example.net
To: <gray@example.org>
Subject: Send file test 2
Date: now
X-Mailer: MH (AT_PACKAGE_STRING)

Message body 2

./,input.1
./,input.2
])

MH_CHECK([send -draft],[send02 send-draft],[
MH_MTSTAILOR()
AT_DATA([Mail/draft],
[From: mhtester@example.net
To: gray@example.net
Subject: Send file test
------
Message body
])
send -draft || exit $?
sed -e '/^Date:/s/:.*$/: now/' -e '/^LENGTH: /s/: [[0-9]][[0-9]]*/: X/' $MAIL_DUMP
echo == Mail/,draft ==
cat Mail/,draft
],
[0],
[MSGID: 0001
SENDER: mhtester@example.net
NRCPT: 1
RCPT[[0]]: <gray@example.net>
LENGTH: X
From: mhtester@example.net
To: <gray@example.net>
Subject: Send file test
Date: now
X-Mailer: MH (AT_PACKAGE_STRING)

Message body

== Mail/,draft ==
From: mhtester@example.net
To: gray@example.net
Subject: Send file test
------
Message body
])

MH_CHECK([send -draftfolder],
[send03 send-draftfolder draftfolder draftmessage],[
MH_MTSTAILOR()
mkdir Mail/drafts
AT_DATA([Mail/drafts/2],
[From: mhtester@example.net
To: gray@example.net
Subject: Draftfolder test

Message body
])
echo "cur: 2" > Mail/drafts/.mh_sequences

send -draftfolder +drafts || echo $?
sed -e '/^Date:/s/:.*$/: now/' -e '/^LENGTH: /s/: [[0-9]][[0-9]]*/: X/' $MAIL_DUMP
],
[0],
[MSGID: 0001
SENDER: mhtester@example.net
NRCPT: 1
RCPT[[0]]: <gray@example.net>
LENGTH: X
From: mhtester@example.net
To: <gray@example.net>
Subject: Draftfolder test
Date: now
X-Mailer: MH (AT_PACKAGE_STRING)

Message body

])

MH_CHECK([send -draftmessage],
[send04 send-draftmessage draftfolder draftmessage],[
MH_MTSTAILOR()
mkdir Mail/drafts
AT_DATA([Mail/drafts/2],
[From: mhtester@example.net
To: gray@example.net
Subject: Draftfolder test

Message body
])

AT_DATA([Mail/drafts/5],
[From: mhtester@example.net
To: gray@example.org
Subject: Draftmessage test

Message body
])
echo "cur: 2" > Mail/drafts/.mh_sequences

send -draftfolder +drafts -draftmessage 5 || echo $?
sed -e '/^Date:/s/:.*$/: now/' -e '/^LENGTH: /s/: [[0-9]][[0-9]]*/: X/' $MAIL_DUMP
],
[0],
[MSGID: 0001
SENDER: mhtester@example.net
NRCPT: 1
RCPT[[0]]: <gray@example.org>
LENGTH: X
From: mhtester@example.net
To: <gray@example.org>
Subject: Draftmessage test
Date: now
X-Mailer: MH (AT_PACKAGE_STRING)

Message body

])

m4_popdef([MH_KEYWORDS])

# End of send.at
